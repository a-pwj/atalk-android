apply plugin: 'com.android.library'

android {
	compileSdkVersion rootProject.compileSdkVersion
	buildToolsVersion rootProject.buildToolsVersion

	defaultConfig {
		versionCode 100
		versionName '0.1.0'

		minSdkVersion rootProject.minSdkVersion
		targetSdkVersion rootProject.compileSdkVersion
	}

	// Do not abort build if lint finds errors
	lintOptions {
		abortOnError false
	}
}

configurations {
	jarjar
	jarjardeps
}

task repackageTask(type: Jar) { repackJar ->
	repackJar.baseName = "ews-android-api-repackaged-dependencies"

	doLast() {
		project.ant {
			taskdef name: "jarjar", classname: "com.tonicsystems.jarjar.JarJarTask",
					classpath: configurations.jarjar.asPath

			jarjar(destfile: repackJar.archivePath) {
				configurations.jarjardeps.each { originalJar ->
					zipfileset(src: originalJar)
				}

				rule pattern: "javax.xml.**", 		result: "android.javax.xml.@1"
				rule pattern: "javax.naming.**",	result: "android.javax.naming.@1"
				zap  pattern: "com.sun.naming.internal.**"
			}
		}
	}
}

dependencies {
	jarjar 'com.googlecode.jarjar:jarjar:1.3'

	jarjardeps fileTree(dir: 'libs', include: ['jndi-1.2.1.jar'])
	jarjardeps fileTree(dir: 'libs', include: ['jsr173_1.0_api.jar'])
	api(files(repackageTask))

	implementation 'joda-time:joda-time:2.8'
}

